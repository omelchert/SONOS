# FILE: SONOS_NF_FF.gpi
#
# FIGURE CAPTION (DRAFT):
#
# (Color online) Fast solver for the photoacoustic problem
# for layered media.  The data curves labeled by $p_{\rm D}(t)$ refer to an
# implementation in cylindrical polar coordinates according to Eq.\
# \ref{eq:PAPoissonEq_D}. The curves are computed for a field point in the
# acoustic near-field (NF; red line) and far-field (FF; blue line) at $z_{\rm
# D}=-0.04~{\rm cm}$ and $z_{\rm D}=-4.0~{\rm cm}$ on the beam axis,
# respectively. 
# (a) Setup where the source-volume encloses two absorbing layers consisting 
# of $\mu_a=10~\rm{cm^{-1}}$ in the range $z=0.0-0.05~{\rm cm}$ (light-gray 
# shaded region) followed by $\mu_a=20~\rm{cm^{-1}}$ in the range 
# $z=0.05-0.1~{\rm cm}$ (gray shaded region), and, 
# (b) setup where the order of the layers is reversed. 
#
# AUTHOR: O. Melchert
# DATE:   21.03.2016
set terminal postscript enhanced eps color lw 1.5 22 font "Times-Roman"
set output "./FIGS/SONOS_NF_FF.eps"

datFile_th111 = "../cyl_2Layer_low_high_zD0.04.dat"
datFile_th112 = "../cyl_2Layer_low_high_zD4.0.dat"
datFile_th121 = "../cyl_2Layer_high_low_zD0.04.dat"
datFile_th122 = "../cyl_2Layer_high_low_zD4.0.dat"

set label 100 "(a)" at screen 0.01,0.67 font "Times-Roman"
set label 101 "(b)" at screen 0.01,0.37 font "Times-Roman"
set label 103 "{/Times-Roman=100 DRAFT}" at screen 0.48,0.35 center rotate by 25 tc "grey" front 
set label 1   "{/Times-Italic c}{/Symbol t} [cm]" right at screen 0.82,0.04
set label 22  "{/Times-Italic p}({/Times-Italic t}) [a.u.]" rotate by 90 right at screen 0.025,0.62
set label 23  "{/Times-Italic p}({/Times-Italic t}) [a.u.]" rotate by 90 right at screen 0.025,0.32

set style line 1 lw 1.5 lt 1 lc rgb "#AA0000" dashtype 1
set style line 2 lw 1.5 lt 1 lc rgb "#0000AA" dashtype 1
set style line 3 lw 1.5 lt 1 lc rgb "black" dashtype "-"
set style line 4 lw 1.5 lt 1 lc rgb "grey" dashtype "."
set style rect fc lt -1 fs solid 0.15 noborder 
set style rect fc lt -1 fs solid 0.15 noborder 

set key samplen 1.20 at graph 1.05,0.96 reverse Left
set xr [-0.025:0.20]
set yr [-1.1:1.3]
set ytics (-1.,-0.5,0.,0.5,1.0)
set tics nomirror
set xzeroaxis
unset x2tics

set lmargin at screen 0.15
set rmargin at screen 0.82
set size 0.85,0.7
set multiplot

# FIG 2 (a): Setup 1
        set bmargin at screen 0.38
        set tmargin at screen 0.68
        set xtics ( "" 0.0, "" 0.05, "" 0.1, "" 0.15 ) 

        set obj 21 rect from 0.0, graph 0 to 0.05, graph 1 fc "#AAAAAA" behind
        set obj 22 rect from 0.05, graph 0 to 0.1, graph 1 fc "#111111" behind

        p datFile_th111 u ($2):($3/0.06) w l ls 1 t "{/Times-Italic p}_{/Times-Roman=14 D}({/Times-Italic t}) - NF"\
        , datFile_th112 u ($2):($3/0.06) w l ls 2 t "{/Times-Italic p}_{/Times-Roman=14 D}({/Times-Italic t}) - FF"\

# FIG 2 (b): Setup 2
        set bmargin at screen 0.08
        set tmargin at screen 0.38
        set xtics ( -0.1, 0.0, 0.05, 0.1, 0.15) 
        unset key

        set obj 31 rect from 0.0, graph 0 to 0.05, graph 1 fc "#111111" behind
        set obj 32 rect from 0.05, graph 0 to 0.1, graph 1 fc "#AAAAAA" behind

        p datFile_th121 u ($2):($3/0.1) w l ls 1 t "NF"\
        , datFile_th122 u ($2):($3/0.1) w l ls 2 t "FF"\

unset obj 31
unset obj 32

unset multiplot
# EOF: SONOS_NF_FF.gpi
